apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "pg.autyzm.friendly_plans"
        minSdkVersion 18
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    adbOptions {
        timeOutInMs 20 * 60 * 1000
        installOptions "-d","-t"
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

greendao {
    schemaVersion 4
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.android.support:appcompat-v7:24.2.0'
    implementation "com.google.dagger:dagger:2.9"
    implementation 'com.nbsp:library:1.2'
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.hamcrest:hamcrest-core:1.3'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.databinding:library:1.3.1'
    implementation 'com.android.databinding:adapters:1.3.1'

    annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    testImplementation "org.robolectric:robolectric:3.5.1"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'com.github.fabioCollini:DaggerMock:0.6.4'
    testImplementation "org.powermock:powermock-module-junit4:1.6.4"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.6.4"
    testImplementation "org.powermock:powermock-api-mockito:1.6.4"
    testImplementation "org.powermock:powermock-classloading-xstream:1.6.4"
    testImplementation "com.google.dagger:dagger:2.9"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:2.9"
    testCompileOnly 'javax.annotation:jsr250-api:1.0'

    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.1'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestImplementation 'com.android.support:support-annotations:25.1.0'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.+'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "debug") {
        def aptOutputDir = new File(buildDir, "generated/source/apt/${variant.unitTestVariant.dirName}")
        variant.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
        assembleDebug.finalizedBy('assembleDebugUnitTest')
    }
}
